# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Maikel Wever <maikelwever@gmail.com>
# Contributor: Asterios Dimitriou <asterios@pci.gr>
# Contributor: Benjamin Asbach <archlinux-aur.lxd@impl.it>
# Contributer: nightuser <nightuser.android at gmail.com>

pkgname=lxd
_pkgname=lxd
_lxd=github.com/lxc/lxd
pkgver=3.19
pkgrel=3
pkgdesc="REST API, command line tool and OpenStack integration plugin for LXC."
arch=('x86_64')
url="https://github.com/lxc/lxd"
license=('APACHE')
depends=('lxc' 'lxcfs' 'squashfs-tools' 'dnsmasq' 'dqlite' 'libuv' 'sqlite-replication')
makedepends=('go' 'git' 'tcl')
optdepends=(
    'lvm2: for lvm2 support'
    'thin-provisioning-tools: for thin provisioning support'
    'btrfs-progs: for btrfs storage driver support'
    'ceph: for ceph storage driver support'
    'cdrtools: VM support'
    'qemu: VM support'
    'ovmf: VM support'
)
#"${url}/releases/download/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.asc}
source=("git+${url}.git#commit=f818c6d1c5933d791356f7f19eb33818f464e2df"
        "lxd.socket"
        "lxd.service"
        "lxd.sysusers")
#validpgpkeys=('602F567663E593BCBD14F338C638974D64792D67')
sha256sums=('SKIP'
            '8748ab5e6ee95e10023f32617c27956814e321173798e0bd066ff78999f51ed0'
            'eaaac8de0411fc9cb47a4f5d4290e499814693c54c9f3811dd19bbbcd06b6f62'
            'd0184d9c4bb485e3aad0d4ac25ea7e85ac0f7ed6ddc96333e74fcd393a5b5ec4')


prepare() {
  mkdir -p "${srcdir}/go/src/github.com/lxc"
  ln -rTsf "${_pkgname}" "${srcdir}/go/src/${_lxd}"
}

build() {
  #export GOPATH="${srcdir}/${pkgname}-${pkgver}/_dist"
  export GOPATH="${srcdir}/go"
  cd "${GOPATH}/src/${_lxd}"
  export CGO_CFLAGS="-I/usr/include/sqlite-replication"
  export CGO_LDFLAGS="-L/usr/lib/sqlite-replication -Wl,-R/usr/lib/sqlite-replication"
  export CGO_LDFLAGS_ALLOW='-Wl,-wrap,pthread_create'
  mkdir -p bin
  go get ./...
  go build -trimpath -v -x -tags libsqlite3 -o bin/ ./...
}

package() {
  #cd "$pkgname-$pkgver"
  cd "$pkgname"

  for tool in fuidshift lxc lxc-to-lxd lxd lxd-agent lxd-benchmark lxd-p2c macaroon-identity; do
    install -p -Dm755 "bin/$tool" "${pkgdir}/usr/bin/$tool"
  done

  # Package license
  install -Dm644 "COPYING"  "${pkgdir}/usr/share/licenses/${_pkgname}/LICENCE"

  # systemd files
  install -Dm644 "${srcdir}/lxd.service" "${pkgdir}/usr/lib/systemd/system/lxd.service"
  install -Dm644 "${srcdir}/lxd.socket" "${pkgdir}/usr/lib/systemd/system/lxd.socket"

  # documentation
  mkdir -p "${pkgdir}/usr/share/doc/lxd"
  install -p -Dm644 "doc/"* "${pkgdir}/usr/share/doc/lxd/"

  # Bash completions
  install -p -Dm644 "scripts/bash/lxd-client" "${pkgdir}/usr/share/bash-completion/completions/lxd"

  install -Dm644 "${srcdir}/$pkgname.sysusers" "${pkgdir}/usr/lib/sysusers.d/$pkgname.conf"
}

# vim:set ts=2 sw=2 et:
